DO $$
DECLARE
    tbl RECORD;
    col RECORD;
    query TEXT;
    invalid_count INT;
    lower_bound DATE := current_date - INTERVAL '7 years';
    upper_bound DATE := current_date + INTERVAL '5 years';
BEGIN
    FOR tbl IN
        SELECT table_name
        FROM information_schema.tables
        WHERE table_schema = 'public' AND table_type = 'BASE TABLE'
    LOOP
        FOR col IN
            SELECT column_name, data_type
            FROM information_schema.columns
            WHERE table_schema = 'public'
              AND table_name = tbl.table_name
              AND data_type IN ('date', 'timestamp without time zone', 'timestamp with time zone')
        LOOP
            query := format(
                'SELECT COUNT(*) FROM public.%I WHERE %I IS NOT NULL AND (%I < %L OR %I > %L)',
                tbl.table_name,
                col.column_name,
                col.column_name, lower_bound,
                col.column_name, upper_bound
            );

            EXECUTE query INTO invalid_count;

            IF invalid_count > 0 THEN
                RAISE NOTICE 'Table: %, Column: %, Invalid Rows: %',
                    tbl.table_name, col.column_name, invalid_count;
            END IF;
        END LOOP;
    END LOOP;
END $$;
